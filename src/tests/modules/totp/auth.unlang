string otp
int32 time_offset

control.TOTP.Secret := 'ONSWG4TFOQYTEMZUGU3DOOBZ'

#
#  With no OTP from the user, the module does nothing
#
totp.authenticate

if (!noop) {
	test_fail
}


#
#  Use oathtool to calculate OTP
#
TOTP.From-User := %exec('/usr/bin/oathtool', '--base32', '--totp', 'ONSWG4TFOQYTEMZUGU3DOOBZ')

totp.authenticate

if (!ok) {
	test_fail
}

#
#  Set a time offset and re-auth which will fail
#
time_offset := 600

totp.authenticate {
	reject = 1
}
if !(reject) {
	test_fail
}

#
#  Get the correct OTP for the time offset and re-auth
#
TOTP.From-User := %exec('/usr/bin/oathtool', '--base32', '--totp', 'ONSWG4TFOQYTEMZUGU3DOOBZ', '--now', %date(%{%l + 600}))

totp.authenticate
if !(ok) {
	test_fail
}

#
#  Now set an incorrect OTP and check for reject
#
TOTP.From-User := 'ABCDEF'

totp.authenticate {
	reject = 1
}

if !(reject) {
	test_fail
}

#
#  And an invalid length OTP
#
TOTP.From-User := '12345'

totp.authenticate {
	fail = 1
}

if !(fail) {
	test_fail
}

test_pass
