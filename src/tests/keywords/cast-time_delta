#
#  PRE: if
#
date one
date two
time_delta diff
time_delta diff2
uint32 num

#
#  We can cast a time duration to milliseconds, and get a sane value.
#
if (%cast("ms", 60s) != 60000) {
	test_fail
}

if (%cast("weeks", 7d) != 1) {
	test_fail
}

#
#  Change of scale doesn't affect the numbers.  Internally, the
#  time_deltas are stored as nanoseconds since epoch.  The scale only
#  matters for parsing and printing.
#
if 7d != 1w {
	test_fail
}

#
#  8/7 is about 1.142....  But since the integers are 64-bit,
#  they have a lot more precision than the floating point numbers.
#  As a result, we look for approximate equality.
#
if ((%cast("weeks", 8d) - 1.14285714w) >= 1s) {
	test_fail
}


#
#  And then for dates
#
#  These dates are one minute apart.
#
one = "2024-08-12T00:00:00Z"
two = "2024-08-12T00:01:00Z"

diff = two - one
if (diff != 60s) {
	test_fail
}

#
#  Cast the time delta to milliseconds
#
num = %cast("ms", diff)

#
#  This results in 60, because the conversion to fr_value_box_cast() doesn't pass dst->enumv?
#
if (num != 60000) {
	test_fail
}

success
